@tool
extends StatEffectRegeneration

const FIRE = preload("res://objects/battle/effects/fire/fire.tscn")
const FIRE_BURST := preload("res://objects/battle/effects/fire/fireburst.tscn")
const SFX_FLAMES := preload("res://audio/sfx/battle/cogs/attacks/SA_hot_air.ogg")
const VISUAL_DOT := preload("res://objects/battle/battle_resources/status_effects/resources/fire_sale_visual_dot.tres")


var particles: Node3D

## Poison effects only trigger at round ends
func apply() -> void:
	place_particles(target, FIRE)
	if target is Player:
		manager.s_battle_ending.connect(cleanup)

func place_particles(who: Node3D, particle_scene: PackedScene) -> void:
	var particle_root: Node
	var particle_scale := 1.0
	if who is Cog:
		particle_root = who.body_root
		particle_scale = 4.0
	elif who is Player:
		particle_root = who.toon
	particles = particle_scene.instantiate()
	particle_root.add_child(particles)
	particles.scale = Vector3.ONE * particle_scale
	particles.position.y = 0.05

func renew() -> void:
	# Don't do movie for dead actors
	if not is_instance_valid(target) or target.stats.hp <= 0:
		return
	
	manager.battle_node.focus_character(target)
	manager.affect_target(target, amount)
	if target is Player:
		target.set_animation('cringe')
	else:
		target.set_animation('pie-small')
	await manager.sleep(3.0)
	await manager.check_pulses([target])

func cleanup() -> void:
	expire()
	if manager.s_battle_ending.is_connected(cleanup):
		manager.s_battle_ending.disconnect(cleanup)

func expire() -> void:
	if is_instance_valid(particles):
		particles.queue_free()

func get_status_name() -> String:
	return "Boiling"

func get_description() -> String:
	if not description == "":
		return description
	return "%d damage per round" % amount

func combine(effect: StatusEffect) -> bool:
	if effect.rounds == rounds:
		amount += effect.amount
		return true
	return false

func randomize_effect() -> void:
	super()
	if target:
		amount = ceili(randf_range(ceili(target.stats.max_hp * 0.05), ceili(target.stats.max_hp * 0.25)))
	rounds = -1
